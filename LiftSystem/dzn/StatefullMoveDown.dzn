import IMoveDown.dzn;

component SMoveDown {
    provides IMoveDown p;
    requires IMoveDown homedMove;
    requires IMoveDown regularMove;

    behavior {
        enum HomedState { NOT_HOMED, HOMED };
        enum MoveState { IDLE, MOVING,  ABORTING };
        HomedState homedState = HomedState.NOT_HOMED;
        MoveState moveState = MoveState.IDLE;
        
        [ homedState.NOT_HOMED && moveState.IDLE ] on p.moveDown(): { homedMove.moveDown(); moveState = MoveState.MOVING; }
        [ homedState.NOT_HOMED && moveState.MOVING] on p.abort(): { homedMove.abort(); moveState = MoveState.ABORTING; }
        [ homedState.NOT_HOMED && moveState.MOVING ] on homedMove.moveFinished(): { p.moveFinished(); moveState = MoveState.IDLE; homedState = HomedState.HOMED; }
        [ homedState.NOT_HOMED && moveState.ABORTING ] on homedMove.moveAborted(): { p.moveAborted(); moveState = MoveState.IDLE; }

        [ homedState.HOMED && moveState.IDLE ] on p.moveDown(): { regularMove.moveDown(); moveState = MoveState.MOVING; }
        [ homedState.HOMED && moveState.MOVING] on p.abort(): { regularMove.abort(); moveState = MoveState.ABORTING; }
        [ homedState.HOMED && moveState.MOVING ] on regularMove.moveFinished(): { p.moveFinished(); moveState = MoveState.IDLE; }
        [ homedState.HOMED && moveState.ABORTING ] on regularMove.moveAborted(): { p.moveAborted(); moveState = MoveState.IDLE; }
    }
}